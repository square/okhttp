import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id "com.palantir.graal" version "0.7.2"
}

dependencies {
  implementation deps.assertj
  implementation deps.junit5Api
  implementation deps.junit5JupiterEngine
  implementation deps.junitPlatformConsole

  implementation project(':okhttp')
  implementation project(':okhttp-brotli')
  implementation project(':okhttp-dnsoverhttps')
  implementation project(':okhttp-logging-interceptor')
  implementation project(':okhttp-sse')
  implementation project(':okhttp-testing-support')
  implementation project(':okhttp-tls')
  implementation deps.assertj
  implementation project(':mockwebserver')
  implementation project(':mockwebserver-deprecated')
  implementation project(':okhttp-urlconnection')
  implementation project(':mockwebserver-junit4')
  implementation project(':mockwebserver-junit5')
  implementation deps.bndResolve
  implementation deps.junit5Api
  implementation deps.junit5JupiterParams

  implementation("org.graalvm.nativeimage:svm:20.3.0") {
    // https://youtrack.jetbrains.com/issue/KT-29513
    exclude group: "org.graalvm.nativeimage"
    exclude group: "org.graalvm.truffle"
    //    exclude group= "org.graalvm.sdk"
    exclude group: "org.graalvm.compiler"
  }

  compileOnly deps.jsr305
}

animalsniffer {
  ignoreFailures = true
}

def isIDE = properties.containsKey('android.injected.invoked.from.ide') ||
        (System.getenv("XPC_SERVICE_NAME") ?: "").contains("intellij") ||
        System.getenv("IDEA_INITIAL_DIRECTORY") != null
if (!isIDE) {
  sourceSets {
    // Not included in IDE as this confuses Intellij for obvious reasons.
    main.java.srcDirs += project.file("../okhttp/src/test/java")
    main.java.srcDirs += project.file("../okhttp-brotli/src/test/java")
    main.java.srcDirs += project.file("../okhttp-dnsoverhttps/src/test/java")
    main.java.srcDirs += project.file("../okhttp-logging-interceptor/src/test/java")
    main.java.srcDirs += project.file("../okhttp-sse/src/test/java")
  }
}

graal {
  mainClass "okhttp3.RunTestsKt"
  outputName "ConsoleLauncher"
  graalVersion "20.3.0"
  javaVersion "11"

  option "--no-fallback"
  option "--allow-incomplete-classpath"
  option "--report-unsupported-elements-at-runtime"
  option "-H:+ReportExceptionStackTraces"

  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // May be possible without, but autodetection is problematic on Windows 10
    // see https://github.com/palantir/gradle-graal
    // see https://www.graalvm.org/docs/reference-manual/native-image/#prerequisites
    windowsVsVarsPath('C:\\Program Files (x86)\\Microsoft Visual Studio\\2019\\BuildTools\\VC\\Auxiliary\\Build\\vcvars64.bat')
  }
}
