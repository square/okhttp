apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'me.champeau.gradle.japicmp'

jar {
  manifest {
    attributes('Automatic-Module-Name': 'okhttp3.tls')
  }
}

dependencies {
  api deps.okio
  implementation project(':okhttp')
  implementation deps.bouncycastle
  compileOnly deps.jsr305

  testImplementation project(':okhttp-testing-support')
  testImplementation deps.junit
  testImplementation deps.assertj
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/4.x"
    outputFormat = 'gfm'
  }
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(baselineJar(project, baselineVersion))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
  packageExcludes = [
      'okhttp3.tls.internal',
  ]
  methodExcludes = [
      // Became 'final' in 4.0.0:
      'okhttp3.tls.HandshakeCertificates#keyManager()',
      'okhttp3.tls.HandshakeCertificates#sslContext()',
      'okhttp3.tls.HandshakeCertificates#sslSocketFactory()',
      'okhttp3.tls.HandshakeCertificates#trustManager()',
      'okhttp3.tls.HandshakeCertificates$Builder#addPlatformTrustedCertificates()',
      'okhttp3.tls.HandshakeCertificates$Builder#addTrustedCertificate(java.security.cert.X509Certificate)',
      'okhttp3.tls.HandshakeCertificates$Builder#build()',
      'okhttp3.tls.HandshakeCertificates$Builder#heldCertificate(okhttp3.tls.HeldCertificate, java.security.cert.X509Certificate\\[\\])',
      'okhttp3.tls.HeldCertificate#certificate()',
      'okhttp3.tls.HeldCertificate#certificatePem()',
      'okhttp3.tls.HeldCertificate#keyPair()',
      'okhttp3.tls.HeldCertificate#privateKeyPkcs1Pem()',
      'okhttp3.tls.HeldCertificate#privateKeyPkcs8Pem()',
      'okhttp3.tls.HeldCertificate$Builder#addSubjectAlternativeName(java.lang.String)',
      'okhttp3.tls.HeldCertificate$Builder#build()',
      'okhttp3.tls.HeldCertificate$Builder#certificateAuthority(int)',
      'okhttp3.tls.HeldCertificate$Builder#commonName(java.lang.String)',
      'okhttp3.tls.HeldCertificate$Builder#duration(long, java.util.concurrent.TimeUnit)',
      'okhttp3.tls.HeldCertificate$Builder#ecdsa256()',
      'okhttp3.tls.HeldCertificate$Builder#keyPair(java.security.KeyPair)',
      'okhttp3.tls.HeldCertificate$Builder#keyPair(java.security.PublicKey, java.security.PrivateKey)',
      'okhttp3.tls.HeldCertificate$Builder#organizationalUnit(java.lang.String)',
      'okhttp3.tls.HeldCertificate$Builder#rsa2048()',
      'okhttp3.tls.HeldCertificate$Builder#serialNumber(java.math.BigInteger)',
      'okhttp3.tls.HeldCertificate$Builder#serialNumber(long)',
      'okhttp3.tls.HeldCertificate$Builder#signedBy(okhttp3.tls.HeldCertificate)',
      'okhttp3.tls.HeldCertificate$Builder#validityInterval(long, long)',
  ]
}
check.dependsOn(japicmp)
