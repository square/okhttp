apply plugin: 'me.champeau.gradle.japicmp'
applyOsgi(this)

jar {
  // MANIFEST.MF, including OSGi bnd instructions.
  bnd '''
  Export-Package: okhttp3.sse
  Automatic-Module-Name: okhttp3.sse
  Bundle-SymbolicName: com.squareup.okhttp3.sse
  '''
}

dependencies {
  api project(':okhttp')
  compileOnly deps.jsr305

  testImplementation project(':okhttp-testing-support')
  testImplementation project(':mockwebserver')
  testImplementation project(':mockwebserver-junit5')
  testImplementation deps.junit
  testImplementation deps.assertj
  testCompileOnly deps.jsr305
}

afterEvaluate { project ->
  project.tasks.dokka {
    outputDirectory = "$rootDir/docs/4.x"
    outputFormat = 'gfm'
  }
}

task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(baselineJar(project, baselineVersion))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
  packageExcludes = [
      'okhttp3.internal.sse',
  ]
}
check.dependsOn(japicmp)
