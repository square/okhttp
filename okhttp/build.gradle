apply plugin: 'com.vanniktech.maven.publish'

jar {
  manifest {
    attributes('Automatic-Module-Name': 'okhttp3')
  }
}

sourceSets {
  main.java.srcDirs += "$buildDir/generated/sources/java-templates/java/main"
}

compileJava {
  dependsOn 'copyJavaTemplates'
}

task copyJavaTemplates(type: Copy) {
  from 'src/main/java-templates'
  into "$buildDir/generated/sources/java-templates/java/main"
  expand('projectVersion': "$VERSION_NAME")
  filteringCharset = 'UTF-8'
}

dependencies {
  api deps.okio
  api deps.kotlinStdlib
  compileOnly deps.conscrypt
  compileOnly deps.android
  compileOnly deps.jsr305
  compileOnly deps.animalSniffer

  testImplementation deps.junit
  testImplementation deps.assertj
}

apply plugin: 'me.champeau.gradle.japicmp'
task japicmp(type: me.champeau.gradle.japicmp.JapicmpTask, dependsOn: 'jar') {
  oldClasspath = files(baselineJar(project, baselineVersion))
  newClasspath = files(jar.archivePath)
  onlyBinaryIncompatibleModified = true
  failOnModification = true
  txtOutputFile = file("$buildDir/reports/japi.txt")
  ignoreMissingClasses = true
  includeSynthetic = true
  methodExcludes = [
      'okhttp3.TlsVersion#javaName()', // Became 'final' in 4.0.0.
  ]
}
check.dependsOn(japicmp)
