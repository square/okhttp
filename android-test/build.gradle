apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

repositories {
  jcenter {
    // Required for a dependency of Android lint.
    content {
      includeGroup 'org.jetbrains.trove4j'
    }
  }
}

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  compileSdkVersion 29

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArguments(['notClass': 'org.conscrypt.KitKatPlatformOpenSSLSocketImplAdapter'])
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
  implementation "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}"
  implementation project(':okhttp')
  testImplementation 'junit:junit:4.12'
  androidTestImplementation(project(':okhttp-testing-support')) {
    exclude group: 'org.openjsse', module: 'openjsse'
    exclude group: 'org.conscrypt', module: 'conscrypt-openjdk-uber'
    exclude group: 'software.amazon.cryptools', module: 'AmazonCorrettoCryptoProvider'
  }
  androidTestImplementation "org.conscrypt:conscrypt-android:2.2.1"
  androidTestImplementation project(':mockwebserver')
  androidTestImplementation project(':okhttp-tls')
  androidTestImplementation project(':okhttp-dnsoverhttps')
  androidTestImplementation project(':okhttp-logging-interceptor')
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
  androidTestImplementation 'com.squareup.moshi:moshi:1.8.0'
  androidTestImplementation 'com.squareup.moshi:moshi-kotlin:1.8.0'
}
