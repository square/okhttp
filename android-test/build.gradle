apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: "de.mannodermaus.android-junit5"

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_11
  }

  packagingOptions {
    // issue merging due to conflict with httpclient and something else
    exclude("META-INF/DEPENDENCIES")
    exclude("META-INF/LICENSE.md")
    exclude("META-INF/LICENSE-notice.md")
    exclude("README.txt")
    exclude("org/bouncycastle/LICENSE")
  }

  compileSdkVersion 31

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 31
    versionCode 1
    versionName "1.0"

    // Make sure to use the AndroidJUnitRunner (or a sub-class) in order to hook in the JUnit 5 Test Builder
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testInstrumentationRunnerArguments([
            'runnerBuilder': 'de.mannodermaus.junit5.AndroidJUnit5Builder',
            'notPackage'   : 'org.bouncycastle'
    ])
  }

  sourceSets {
    named("androidTest") {
      it.java.srcDirs += project.file("../okhttp-brotli/src/test/java")
      it.java.srcDirs += project.file("../okhttp-dnsoverhttps/src/test/java")
      it.java.srcDirs += project.file("../okhttp-logging-interceptor/src/test/java")
      it.java.srcDirs += project.file("../okhttp-sse/src/test/java")
    }
  }
}

dependencies {
  implementation Dependencies.kotlinReflect
  implementation Dependencies.playServicesSafetynet
  implementation project(':okhttp')

  androidTestImplementation(project(':okhttp-testing-support')) {
    exclude group: 'org.openjsse', module: 'openjsse'
    exclude group: 'org.conscrypt', module: 'conscrypt-openjdk-uber'
    exclude group: 'software.amazon.cryptools', module: 'AmazonCorrettoCryptoProvider'
  }
  androidTestImplementation Dependencies.bouncycastle
  androidTestImplementation Dependencies.bouncycastletls
  androidTestImplementation Dependencies.conscryptAndroid
  androidTestImplementation project(':mockwebserver-junit5')
  androidTestImplementation project(':okhttp-brotli')
  androidTestImplementation project(':okhttp-dnsoverhttps')
  androidTestImplementation project(':okhttp-logging-interceptor')
  androidTestImplementation project(':okhttp-sse')
  androidTestImplementation project(':okhttp-testing-support')
  androidTestImplementation project(':okhttp-tls')
  androidTestImplementation Dependencies.androidxExtJunit
  androidTestImplementation Dependencies.androidxEspressoCore
  androidTestImplementation Dependencies.httpclient5
  androidTestImplementation Dependencies.moshi
  androidTestImplementation Dependencies.moshiKotlin
  androidTestImplementation Dependencies.okioFakeFileSystem

  androidTestImplementation Dependencies.androidxTestRunner
  androidTestImplementation Dependencies.junit5Api
  androidTestImplementation Dependencies.junit5AndroidCore
  androidTestRuntimeOnly Dependencies.junit5AndroidRunner
}
