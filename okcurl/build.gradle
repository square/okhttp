plugins {
  id "com.palantir.graal" version "0.3.0-6-g0b828af"
  id "com.hpe.kraal" version "0.0.15"
}

apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.github.johnrengelman.shadow'

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'okhttp3.curl'
    attributes 'Main-Class': 'okhttp3.curl.Main'
  }
}

// resources-templates.
sourceSets {
  main.resources.srcDirs += "$buildDir/generated/resources-templates"
}
compileJava {
  dependsOn 'copyResourcesTemplates'
}

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict', '-Xjvm-default=enable']
    jvmTarget = '1.8'
    apiVersion = "1.3"
    languageVersion = "1.3"
    allWarningsAsErrors = false
  }
}

task copyResourcesTemplates(type: Copy) {
  from 'src/main/resources-templates'
  into "$buildDir/generated/resources-templates"
  expand('projectVersion': "$VERSION_NAME")
  filteringCharset = 'UTF-8'
}

dependencies {
  api project(':okhttp')
  api project(':okhttp-logging-interceptor')
  implementation deps.airline
  implementation deps.guava

  testImplementation project(':okhttp-testing-support')
  testImplementation deps.junit
  testImplementation deps.assertj
}

shadowJar {
  mergeServiceFiles()
}

graal {
  graalVersion("1.0.0-rc15")
  mainClass("okhttp3.curl.Main")
  outputName("okcurl")
  option("--enable-http")
  option("--enable-https")
  option("-H:+ReportUnsupportedElementsAtRuntime")
  option("-H:+ReportExceptionStackTraces")
  option("-H:ReflectionConfigurationFiles=reflect.config")
}
