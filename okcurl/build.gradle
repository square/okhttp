plugins {
  id "com.palantir.graal" version "0.3.0-6-g0b828af"
  id "com.hpe.kraal" version "0.0.15"
  id  "application"
}

apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.github.johnrengelman.shadow'

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'okhttp3.curl'
    attributes 'Main-Class': 'okhttp3.curl.Main'
  }
}

application {
  mainClassName = "okhttp3.curl.Main"
}

// resources-templates.
sourceSets {
  main.resources.srcDirs += "$buildDir/generated/resources-templates"
}
compileJava {
  dependsOn 'copyResourcesTemplates'
}

compileKotlin {
  kotlinOptions {
    freeCompilerArgs = ['-Xjsr305=strict', '-Xjvm-default=enable']
    jvmTarget = '1.8'
    apiVersion = "1.3"
    languageVersion = "1.3"
    allWarningsAsErrors = false
  }
}

task copyResourcesTemplates(type: Copy) {
  from 'src/main/resources-templates'
  into "$buildDir/generated/resources-templates"
  expand('projectVersion': "$VERSION_NAME")
  filteringCharset = 'UTF-8'
}

dependencies {
  api project(':okhttp')
  api project(':okhttp-logging-interceptor')
  implementation deps.picocli
  implementation "info.picocli:picocli-codegen:4.0.1"
  implementation deps.guava
  implementation "org.conscrypt:conscrypt-openjdk-uber:${versions.conscrypt}"
  implementation "org.bouncycastle:bcprov-jdk15on:${versions.bouncycastle}"
  implementation "org.bouncycastle:bctls-jdk15on:${versions.bouncycastle}"

  testImplementation project(':okhttp-testing-support')
  testImplementation deps.junit
  testImplementation deps.assertj
}

shadowJar {
  mergeServiceFiles()
}

graal {
  graalVersion("19.1.1")
  downloadBaseUrl("https://github.com/oracle/graal/releases/download/vm-19.1.1/graalvm-ce-darwin-amd64-19.1.1.tar.gz?a=")
  mainClass("okhttp3.curl.Main")
  outputName("okcurl")
  option("--enable-http")
  option("--enable-https")
  option("-H:+ReportUnsupportedElementsAtRuntime")
  option("-H:+ReportExceptionStackTraces")
  option("-H:ReflectionConfigurationFiles=reflect.config")
  option("--rerun-class-initialization-at-runtime=org.bouncycastle.crypto.prng.SP800SecureRandom")
  option("--rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG\$Default")
  option("--rerun-class-initialization-at-runtime=org.bouncycastle.jcajce.provider.drbg.DRBG\$NonceAndIV")
  option("--initialize-at-build-time=org.bouncycastle.util.Strings")
//  option("-J-Djava.security.properties=java.security.overrides")
  option("-J-Djava.net.preferIPv4Stack=true")
}
