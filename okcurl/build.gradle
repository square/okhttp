plugins {
  id "com.palantir.graal" version "0.7.2"
}

import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.github.johnrengelman.shadow'

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'okhttp3.curl'
    attributes 'Main-Class': 'okhttp3.curl.Main'
  }
}

// resources-templates.
sourceSets {
  main.resources.srcDirs += "$buildDir/generated/resources-templates"
}
compileJava {
  dependsOn 'copyResourcesTemplates'
}
task copyResourcesTemplates(type: Copy) {
  from 'src/main/resources-templates'
  into "$buildDir/generated/resources-templates"
  expand('projectVersion': "${project.version}")
  filteringCharset = 'UTF-8'
}

dependencies {
  api project(':okhttp')
  api project(':okhttp-logging-interceptor')
  implementation deps.picocli
  implementation deps.guava

  implementation 'info.picocli:picocli-codegen:4.5.2'
  annotationProcessor 'info.picocli:picocli-codegen:4.5.2'

  testImplementation project(':okhttp-testing-support')
  testImplementation deps.junit5Api
  testImplementation deps.assertj
}

shadowJar {
  mergeServiceFiles()
}

graal {
  mainClass "okhttp3.curl.Main"
  outputName "okcurl"
  graalVersion "20.3.0"
  javaVersion "11"

  option "--no-fallback"
  option "--allow-incomplete-classpath"

  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // May be possible without, but autodetection is problematic on Windows 10
    // see https://github.com/palantir/gradle-graal
    // see https://www.graalvm.org/docs/reference-manual/native-image/#prerequisites
    windowsVsVarsPath("C:\\Program Files (x86)\\Microsoft Visual Studio\\2019\\BuildTools\\VC\\Auxiliary\\Build\\vcvars64.bat")
  }
}
